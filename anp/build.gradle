plugins {
	id 'eclipse'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

eclipse {
	project {
		name = 'anp'
		comment = 'An annotation processor that builds constant trees'
	}
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
	
	implementation project(':api')

	// https://github.com/square/javapoet
	implementation 'com.squareup:javapoet:1.12.0'
}

test {
	useJUnitPlatform()
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
	from javadoc
	archiveClassifier = 'javadoc'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = 'com.github.cilki'
			artifactId = 'tree-constants'
			version = TREECONSTANTS_VERSION

			from components.java
			artifact sourcesJar
			artifact javadocJar
			pom {
				name = 'TreeConstants Annotation Processor'
				description = 'An annotation processor that builds constant trees'
				url = 'https://github.com/cilki/TreeConstants'
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				developers {
					developer {
						id = 'cilki'
						name = 'Tyler Cook'
						email = 'tcc@sandpolis.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/cilki/TreeConstants.git'
					developerConnection = 'scm:git:ssh://git@github.com/cilki/TreeConstants.git'
					url = 'https://github.com/cilki/TreeConstants'
				}
			}
		}
	}
	repositories {
		maven {
			url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			credentials {
				username PUBLISH_USER
				password PUBLISH_PASS
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
